use crate::lang::Ranged;
use crate::lang::surface::{Literal, Pattern, PatternData, Term, TermData, TypeEntry, TermEntry};
use crate::lang::surface::lexer::{LexerError, Token};

grammar<'input>;

extern {
    type Location = usize;
    type Error = LexerError;

    enum Token<'input> {
        "doc comment" => Token::DocComment(<&'input str>),
        "character literal" => Token::CharLiteral(<&'input str>),
        "string literal" => Token::StrLiteral(<&'input str>),
        "numeric literal" => Token::NumLiteral(<&'input  str>),
        "name" => Token::Name(<&'input str>),
        "shift" => Token::Shift(<&'input str>),

        "as" => Token::As,
        "fun" => Token::FunTerm,
        "Fun" => Token::FunType,
        "record" => Token::RecordTerm,
        "Record" => Token::RecordType,

        "->"  => Token::Arrow,
        "=>"  => Token::DArrow,
        ":"   => Token::Colon,
        ","   => Token::Comma,
        "."   => Token::Dot,

        "{" => Token::LBrace,
        "}" => Token::RBrace,
        "[" => Token::LBrack,
        "]" => Token::RBrack,
        "(" => Token::LParen,
        ")" => Token::RParen,
        "=" => Token::Equal,
    }
}

pub Term: Term<&'input str> = Ranged<TermData>;
ArrowTerm: Term<&'input str> = Ranged<ArrowTermData>;
ExprTerm: Term<&'input str> = Ranged<ExprTermData>;
AppTerm: Term<&'input str> = Ranged<AppTermData>;
AtomicTerm: Term<&'input str> = Ranged<AtomicTermData>;

TermData: TermData<&'input str> = {
    ExprTermData,
    <term: ExprTerm> ":" <ty: Term> => TermData::Ann(Box::new(term), Box::new(ty)),
};

ExprTermData: TermData<&'input str> = {
    ArrowTermData,
    "fun" <input_patterns: AtomicPattern+> "=>" <output_term: ExprTerm> => {
        TermData::FunctionTerm(input_patterns, Box::new(output_term))
    },
};

ArrowTermData: TermData<&'input str> = {
    AppTermData,
    "Fun" <input_patterns: AtomicPattern+> "->" <output_type: ArrowTerm> => {
        TermData::FunctionType(input_type_groups, Box::new(output_type))
    },
    <input_type: AppTerm> "->" <output_type: ArrowTerm> => {
        TermData::FunctionArrowType(Box::new(input_type), Box::new(output_type))
    },
};

AppTermData: TermData<&'input str> = {
    AtomicTermData,
    <head_term: AtomicTerm> <input_terms: AtomicTerm+> => {
        TermData::FunctionElim(Box::new(head_term), input_terms)
    },
};

AtomicTermData: TermData<&'input str> = {
    "(" <term: TermData> ")" => term,
    <name: Name> => TermData::Name(name),
    <term: AtomicTerm> <shift: "shift"> => {
        TermData::Lift(Box::new(term), shift[1..].parse().unwrap()) // FIXME: Overflow!
    },
    "Record" "{" <entries: List<TypeEntry>> "}" => TermData::RecordType(entries),
    "record" "{" <entries: List<TermEntry>> "}" => TermData::RecordTerm(entries),
    <head_term: AtomicTerm> "." <label: Ranged<Name>> => TermData::RecordElim(Box::new(head_term), label),
    "[" <entries: List<Term>> "]" => TermData::Sequence(entries),
    <literal: Literal> => TermData::Literal(literal),
};

Pattern: Pattern<&'input str> = Ranged<PatternData>;
AtomicPattern: Pattern<&'input str> = Ranged<AtomicPatternData>;

PatternData: PatternData<&'input str> = {
    AtomicPatternData,
    <pattern: AtomicPattern> ":" <ty: Term> => PatternData::Ann(Box::new(term), Box::new(ty)),
};

AtomicPatternData: PatternData<&'input str> = {
    "(" <pattern: AtomicPatternData> ")" => pattern,
    <name: Name> => PatternData::Name(name),
};

List<Entry>: Vec<Entry> = {
    <mut entries: (<Entry> ",")*> <last: Entry?> => {
        entries.extend(last);
        entries
    }
}

TypeEntry: TypeEntry<&'input str> = {
    // TODO: Use doc comments
    <_docs: "doc comment"*>
    <label: Ranged<Name>> <name: ("as" <Ranged<Name>>)?> ":" <term: Term> => (label, name, term),
};

TermEntry: TermEntry<&'input str> = {
    // TODO: Use doc comments
    <_docs: "doc comment"*> <label: Ranged<Name>> "=" <term: Term> => (label, term),
};

Name: &'input str = {
    "name",
};

Literal: Literal<&'input str> = {
    "character literal" => Literal::Char(<>),
    "string literal" => Literal::String(<>),
    "numeric literal" => Literal::Number(<>),
};

Ranged<T>: Ranged<T> = {
    <start: @L> <data: T> <end: @R> => Ranged::new(start..end, data),
};
